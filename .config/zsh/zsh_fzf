source /usr/share/fzf/key-bindings.zsh
bindkey -r '\ec'
bindkey '^f' fzf-cd-widget

cd_fzf()
{
	local ret="$(fd -td -uu -L -E ".git/" -E ".cache/" \
		--base-directory $1 | fzf --preview="tree -L 1 {}" \
		--bind="tab:toggle-preview" --preview-window=:hidden)"
	[[ ! -z "$ret" ]] && cd "$1/$ret"
	zle reset-prompt
}
cd_root() { cd_fzf $HOME }
zle -N cd_root
bindkey '^hf' cd_root

find_fzf()
{
	local ret="$(fd -uu -E ".git/" -E ".cache/" --base-directory $1 \
		| fzf -m | sed -E -e "s|./|$1/|" | sed 's/ /\\ /g' | tr '\n' ' ')"
	[[ ! -z "$ret" ]] && LBUFFER="$LBUFFER$ret"
	zle reset-prompt
}
fzf_root()
{
	find_fzf $HOME
}
zle -N fzf_root
bindkey '^ht' fzf_root

pac_search_remote()
{
	local ret="$(pacman -Sql | fzf -m --preview='pacman -Si {}' \
		| tr '\n' ' ')"
	LBUFFER=${LBUFFER}${ret}
	zle reset-prompt
}
zle -N pac_search_remote
bindkey '^ps' pac_search_remote

pac_search_local()
{
	local ret="$(pacman -Qq | fzf -m --preview='pacman -Qi {}' \
		| tr '\n' ' ')"
	LBUFFER=${LBUFFER}${ret}
	zle reset-prompt
}
zle -N pac_search_local
bindkey '^pq' pac_search_local

pac_search_aur()
{
	local ret="$(aur-search)"
	LBUFFER=${LBUFFER}${ret}
	zle reset-prompt
}
zle -N pac_search_aur
bindkey '^pa' pac_search_aur
