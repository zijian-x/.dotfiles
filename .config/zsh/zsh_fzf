source /usr/share/fzf/key-bindings.zsh

# reset defaults
bindkey -r '^T'
bindkey '^[p' fzf-file-widget

bindkey -r '^R'
bindkey '^[o' fzf-history-widget

cd_fzf()
{
    local ret="$(fd -td -u -L \
	--search-path $1 -0 | fzf --read0 --preview="tree -L 1 {}" \
	--bind="ctrl-space:toggle-preview" --preview-window=:hidden)"

    [[ ! -z "$ret" ]] && cd "$ret"
    vcs_info
    zle reset-prompt
}

cd_fzf_home() { cd_fzf "$HOME" }

zle -N cd_fzf_home && bindkey '^[C' cd_fzf_home

find_fzf()
{
    [[ -z $1 ]] && exit 1
    local ret="$(fd -H --search-path $1 -0 | fzf --read0 -m)"
    if [[ -n "$ret" ]]; then
	ret="$(printf "%q " "$ret")"
	LBUFFER=${LBUFFER}${ret}
    fi
    zle reset-prompt
}

fzf_home() { find_fzf $HOME }

zle -N fzf_home && bindkey '^[P' fzf_home

pac_search_remote()
{
    local ret="$(pacman -Sql | fzf -m --preview='pacman -Si {}' \
	| tr '\n' ' ')"

    LBUFFER=${LBUFFER}${ret}
    zle reset-prompt
}

zle -N pac_search_remote && bindkey '^[a' pac_search_remote

pac_search_local()
{
    local ret="$(pacman -Qq | fzf -m --preview='pacman -Qi {}' \
	| tr '\n' ' ')"

    LBUFFER=${LBUFFER}${ret}
    zle reset-prompt
}

zle -N pac_search_local && bindkey '^[l' pac_search_local

pac_search_aur()
{
    aur_list="/tmp/packages.gz"

    if [[ ! -f "$aur_list" ]]; then
	curl -s "https://aur.archlinux.org/packages.gz" > "$aur_list" ||
	    return 1
    else
	now=$(date +"%s")
	pkg_time=$(stat -c "%Y" "$aur_list")
	diff=$(( now - pkg_time ))
	if [[ $diff -gt 86400 ]]; then
	    rm "$aur_list" &&
	    curl -s "https://aur.archlinux.org/packages.gz" > "$aur_list" ||
	    return 1
	fi
    fi

    local ret="$(zcat "$aur_list" | fzf -m --preview="auracle info {}" |
	tr '\n' ' ')"
    LBUFFER=${LBUFFER}${ret}
    zle reset-prompt
}

zle -N pac_search_aur && bindkey '^[A' pac_search_aur
