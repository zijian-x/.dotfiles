source /usr/share/fzf/key-bindings.zsh
# reset defaults
bindkey -r '^T'
bindkey '^[f' fzf-file-widget

bindkey -r '^R'
bindkey '^[r' fzf-history-widget

cd_fzf()
{
    local ret="$(fd -td -uu -L -E ".git/" -E ".cache/" \
	--base-directory $1 -0 | fzf --read0 --preview="tree -L 1 {}" \
	--bind="ctrl-space:toggle-preview" --preview-window=:hidden)"

    [[ ! -z "$ret" ]] && cd "$1/$ret"
    zle reset-prompt
}
cd_fzf_home() { cd_fzf $HOME }
zle -N cd_fzf_home && bindkey '^[C' cd_fzf_home

find_fzf()
{
    local ret="$(fd -uu -E ".git/" -E ".cache/" --base-directory $1 -0 \
	| fzf --read0 -m | sed -E -e "s|./|$1/|" -e 's/ /\\ /g' | tr '\n' ' ')"

    [[ ! -z "$ret" ]] && LBUFFER="$LBUFFER$ret"
    zle reset-prompt
}
fzf_home() { find_fzf $HOME }
zle -N fzf_home && bindkey '^[F' fzf_home

pac_search_remote()
{
    local ret="$(pacman -Sql | fzf -m --preview='pacman -Si {}' \
	| tr '\n' ' ')"

    LBUFFER=${LBUFFER}${ret}
    zle reset-prompt
}
zle -N pac_search_remote && bindkey '^[a' pac_search_remote

pac_search_local()
{
    local ret="$(pacman -Qq | fzf -m --preview='pacman -Qi {}' \
	| tr '\n' ' ')"

    LBUFFER=${LBUFFER}${ret}
    zle reset-prompt
}
zle -N pac_search_local && bindkey '^[l' pac_search_local

pac_search_aur()
{
    local ret="$(aur-search)"
    LBUFFER=${LBUFFER}${ret}
    zle reset-prompt
}
zle -N pac_search_aur && bindkey '^[A' pac_search_aur
